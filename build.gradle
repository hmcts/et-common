plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.owasp.dependencycheck' version '9.2.0'
    id 'com.github.ben-manes.versions' version '0.51.0'
}

group 'com.github.hmcts'
version '2.1.2'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

// region test coverage

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
}

// endregion

checkstyle {
    maxWarnings = 0
    toolVersion = '10.17.0'
}

// region publishing

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'et-common'
            version project.version
        }
    }
}

test {
    useJUnitPlatform()
    failFast = true
}

ext {
    reformLoggingVersion = '6.1.5'
    log4JVersion = '2.23.1'
    lombokVersion = '1.18.32'
    junitJupiterVersion = '5.10.2'
}

// endregion

dependencies {
    api group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.3'
    implementation group: 'uk.gov.dwp.regex', name: 'postcode-validation', version: '1.1.0'

    api group: 'com.microsoft.azure', name: 'azure-servicebus', version: '3.6.7'
    api group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.5'

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'

    api group: 'org.elasticsearch', name: 'elasticsearch', version: '7.17.22'

    implementation group: 'com.github.hmcts', name: 'et-data-model', version: '3.15.2'
    implementation group: 'com.github.hmcts', name: 'ecm-common', version: '2.0.5'
    implementation group: 'com.github.hmcts', name: 'ecm-data-model', version: '1.3.4'

    implementation 'org.apache.tika:tika-core:2.9.2'

    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: reformLoggingVersion
    implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: reformLoggingVersion
    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.4'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
    implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '3.0.0-RC1'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '7.5.0'

    testImplementation(platform('org.junit:junit-bom:5.10.2'))
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitJupiterVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.10.2'
    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.4.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.26.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.12.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.12.0'
    testImplementation group: 'org.springframework', name: 'spring-test', version: '6.1.10'
    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
}

jar {
    enabled = true
}
