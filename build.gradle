plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'jacoco'
    id 'maven'
}

group 'uk.gov.hmcts.reform'
version '0.1.15'

sourceCompatibility = '11.0'

repositories {

    mavenLocal()
    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }

    maven {
        url 'https://repo.spring.io/libs-milestone'
    }

    jcenter()
    mavenCentral()

    // jitpack should be last resort
    // see: https://github.com/jitpack/jitpack.io/issues/1939
    maven { url 'https://jitpack.io' }
}

// region test coverage

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

// endregion


// region publishing

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'ecm-common'
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'ecm-common'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/ecm-common'
        version {
            name = project.version
        }
    }
}

// endregion


dependencies {

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: '5.0.1'
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.1.2'

    compile group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'

    compile group: 'commons-io', name: 'commons-io', version: '2.8.0'

    compile group: 'com.google.guava', name: 'guava', version: '23.0'

    compile group: 'org.elasticsearch', name: 'elasticsearch', version: '7.9.3'

    compile group: 'com.microsoft.azure', name: 'azure-servicebus', version: '3.4.0', {
        exclude group: 'javax.mail', module: 'mail'
        exclude group: 'net.minidev', module: 'json-smart'
    }

    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    testCompile group: 'io.rest-assured', name: 'rest-assured', version: '4.3.1'
    testCompile group: 'junit', name: 'junit', version: '4.13.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.6.0'
    testCompile group: 'org.springframework', name: 'spring-test', version: '5.2.9.RELEASE'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.18.0'
    testCompile 'pl.pragmatists:JUnitParams:1.1.1'

    testCompileOnly 'org.projectlombok:lombok:1.18.16'
    testAnnotationProcessor "org.projectlombok:lombok:1.18.16"
}

jar {
    enabled = true
}