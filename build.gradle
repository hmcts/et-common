plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.owasp.dependencycheck' version '6.2.2'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'com.jfrog.bintray' version '1.8.5'
}

group 'uk.gov.hmcts.reform'
version '0.1.125'


sourceCompatibility = '11.0'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
    maven { url 'https://jitpack.io' }
}

// region test coverage

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
}

// endregion

checkstyle {
    maxWarnings = 172
    toolVersion = '8.29'
}

// region publishing

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'ecm-common'
            version project.version
        }
    }
}

def versions = [
        reformLogging   : '5.1.6'
]

ext["rest-assured.version"] = '4.4.0'

// endregion


dependencies {
    api group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '4.0.0'
    api group: 'org.apache.poi', name: 'poi-ooxml', version: '5.0.0'
    api group: 'org.elasticsearch', name: 'elasticsearch', version: '7.13.2'
    api group: 'com.microsoft.azure', name: 'azure-servicebus', version: '3.6.4'

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.7'
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    implementation group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformLogging
    implementation group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformLogging
    implementation group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    implementation group: 'commons-io', name: 'commons-io', version: '2.10.0'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
    // CVE-2020-11987, CVE-2020-11988 (batik-1.13)
    // Can be removed when we upgrade poi-ooxml to 6.0
    // https://bz.apache.org/bugzilla/show_bug.cgi?id=65166
    api group: 'org.apache.xmlgraphics', name: 'batik-all', version: '1.14'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.4.0'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.11.2'
    testImplementation group: 'org.springframework', name: 'spring-test', version: '5.3.8'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.19.0'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
}

jar {
    enabled = true
}