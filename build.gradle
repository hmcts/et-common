plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.owasp.dependencycheck' version '7.0.4.1'
    id 'com.github.ben-manes.versions' version '0.42.0'
}

group 'uk.gov.hmcts.reform'
version '0.0.26'

sourceCompatibility = '11.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

// region test coverage

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
}

// endregion

checkstyle {
    maxWarnings = 63
    toolVersion = '8.29'
}

// region publishing

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'et-common'
            version project.version
        }
    }
}

ext {
    reformLoggingVersion = '5.1.9'
    log4JVersion = '2.17.0'
    lombokVersion = '1.18.22'
}

// endregion


dependencies {
    api group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.3'
    api group: 'com.microsoft.azure', name: 'azure-servicebus', version: '3.6.6'
    api group: 'org.apache.poi', name: 'poi-ooxml', version: '5.0.0'
    api('org.apache.xmlgraphics:batik-all:1.14') {
        because 'CVE-2020-11987, CVE-2020-11988. Can be removed when we upgrade poi-ooxml. See https://bz.apache.org/bugzilla/show_bug.cgi?id=65166'
    }
    api group: 'org.elasticsearch', name: 'elasticsearch', version: '7.17.0'

    implementation group: 'com.github.hmcts', name: 'et-data-model', version: '0.0.4'
    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: reformLoggingVersion
    implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: reformLoggingVersion
    implementation group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
    implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '4.5.1'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.22.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.4.0'
    testImplementation group: 'org.springframework', name: 'spring-test', version: '5.3.17'
    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'

    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
}

jar {
    enabled = true
}
