plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.owasp.dependencycheck' version '12.1.0'
    id 'com.github.ben-manes.versions' version '0.52.0'
}

group 'com.github.hmcts'
version System.getenv('RELEASE_VERSION') ?: "DEV-SNAPSHOT-${new Date().format('yyyyMMdd-HHmmss')}"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1" }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.5"
    }
}

// region test coverage

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
}

// endregion

checkstyle {
    maxWarnings = 0
    toolVersion = '10.18.2'
}

// region publishing

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'et-common'
            version project.version
        }
    }

    repositories {
        maven {
            name = "AzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }
}

test {
    useJUnitPlatform()
}

ext {
    reformLoggingVersion = '6.1.9'
    log4JVersion = '2.24.3'
    lombokVersion = '1.18.38'
    junitJupiterVersion = '5.11.4'
}

// endregion

dependencies {
    // Spring Framework dependencies (needed for Spring classes)
    implementation 'org.springframework:spring-web'
    implementation 'org.springframework:spring-context'
    implementation 'org.springframework:spring-webmvc'
    
    // AspectJ dependencies (for @Aspect annotation)
    implementation 'org.aspectj:aspectjrt'
    implementation 'org.aspectj:aspectjweaver'
    
    // Jakarta Annotation API (for @PreDestroy)
    implementation 'jakarta.annotation:jakarta.annotation-api'
    
    api group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.1.2'
    implementation group: 'uk.gov.dwp.regex', name: 'postcode-validation', version: '1.1.0'

    api group: 'com.microsoft.azure', name: 'azure-servicebus', version: '3.6.7'
    api group: 'org.apache.poi', name: 'poi-ooxml', version: '5.4.0'

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'

    api group: 'org.elasticsearch', name: 'elasticsearch', version: '7.17.29'

    implementation group: 'com.github.hmcts', name: 'et-data-model', version: 'ecc-ammendments-218c79b'
    implementation group: 'com.github.hmcts', name: 'ecm-common', version: '3.0.1'
    implementation group: 'com.github.hmcts', name: 'ecm-data-model', version: '2.0.3'

    implementation 'org.apache.tika:tika-core:3.1.0'

    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: reformLoggingVersion
    implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: reformLoggingVersion
    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
    implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '3.0.0-RC1'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '7.10.2'

    testImplementation(platform('org.junit:junit-bom:5.11.4'))
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitJupiterVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.11.4'
    testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.5.6'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.4'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.19.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.19.0'
    testImplementation group: 'org.springframework', name: 'spring-test', version: '6.2.10'
    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
}

jar {
    enabled = true
}

tasks.register('printVersion') {
    doLast {
        println "Publishing version: ${project.version}"
    }
}

publishToMavenLocal.dependsOn printVersion
